
buildscript {
    repositories {
        repositories {
            mavenLocal()
            mavenCentral()
            maven {
                url "https://plugins.gradle.org/m2/"
            }
            google()
        }
    }

    dependencies {
        classpath(libs.kotlin.gradle.plugin)
    }
}

plugins {
    alias(libs.plugins.kotlin.jvm) apply false
    alias(libs.plugins.pleo.gradle.plugin)
}

allprojects {
    group = 'io.pleo'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: libs.plugins.java.library.get().pluginId
    apply plugin: libs.plugins.kotlin.library.get().pluginId
    apply plugin: libs.plugins.maven.publish.get().pluginId

    sourceCompatibility = 17
    targetCompatibility = 17

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
        kotlinOptions {
            jvmTarget = "17"
            javaParameters = true
            languageVersion = "1.6"
        }
    }

    dependencies {
        implementation(libs.slf4j)
        implementation(libs.kotlin.reflect)
        implementation(libs.kotlin.guice)

        testImplementation(libs.google.truth)
        testImplementation(libs.logback.classic)
        testImplementation(libs.junit.jupiter)
        testImplementation(libs.mockk)

        testRuntimeOnly(libs.junit.jupiter.engine)
    }

    javadoc {
        if(JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }

    test {
        useJUnitPlatform()
    }

}

tasks.check {
    finalizedBy("formatKotlin")
}

pleoRepositoryExtension {
    publish.set(subprojects.toSet())
}
