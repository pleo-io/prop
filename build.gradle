allprojects {
    apply plugin: 'maven'

    group = 'io.pleo'
    version = '2.0.0'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    repositories {
        mavenLocal()
        mavenCentral()

//        maven { url "http://repo.maven.apache.org/maven2" }
    }

    dependencies {
        compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.20'
        testCompile group: 'junit', name: 'junit', version: '4.12'
        testCompile group: 'org.mockito', name: 'mockito-core', version: '2.6.8'
        testCompile group: 'com.google.truth', name: 'truth', version: '0.31'
        testCompile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allJava
        classifier = 'sources'
    }

    task javadocJar(type: Jar) {
        from javadoc
        classifier = 'javadoc'
    }

    javadoc {
        if(JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }

    repositories {
        mavenLocal()

        maven {
            credentials {
                username "$mavenUser"
                password "$mavenPassword"
            }
            url "https://maven.pleo.io/artifactory/libs-release"
        }

        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/pleo-io/prop")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }

        mavenCentral()
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                pom {
                    name = project.name
                    description = "Dynamic properties for your Java app"
                    url = 'http://github.com/pleo-io/prop'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'http://www.opensource.org/licenses/mit-license.php'
                        }
                    }
                    developers {
                        developer {
                            name = 'Pleo'
                            email = 'dev@pleo.io'
                        }
                    }
                    scm {
                        connection = 'scm:git:git@github.com:pleo-io/prop.git'
                        developerConnection = 'scm:git:git@github.com:pleo-io/prop.git'
                        url = 'https://github.com/pleo-io/prop'
                    }
                }
            }
        }

        repositories {
            mavenLocal()
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/pleo-io/prop")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                    password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }

}
